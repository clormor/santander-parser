plugins {
    id 'java'
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'signing'
    id 'org.inferred.processors' version '2.1.0'
    id 'com.palantir.circle.style' version '1.1.4'
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'
group = 'io.github.clormor'
version = '0.0.1-SNAPSHOT'

repositories {
    mavenCentral()
}

sourceSets {
    integrationTest {
        compileClasspath += sourceSets.main.output + configurations.testCompile
        runtimeClasspath += output + compileClasspath + configurations.testRuntime
    }
}

dependencies {
    compile "com.google.guava:guava:$guavaVersion"
    compile "joda-time:joda-time:$jodaTimeVersion"
    compile "org.apache.pdfbox:pdfbox:$pdfBoxVersion"
    implementation "org.immutables:value-annotations:$immutablesVersion"
    annotationProcessor "org.immutables:value:$immutablesVersion"
	testCompile "junit:junit:$junitVersion"
}

idea {
    module {
        testSourceDirs += sourceSets.integrationTest.java.srcDirs
        testResourceDirs += sourceSets.integrationTest.resources.srcDirs
        scopes.TEST.plus += [ configurations.integrationTestCompile ]
        inheritOutputDirs = true
        downloadJavadoc = true
        downloadSources = true
        scopes.PROVIDED.plus += [configurations.compileOnly]
        iml {
            whenMerged { module ->
                def compileDeps = module.dependencies.findAll { it.scope == 'COMPILE' }
                compileDeps*.exported = true
            }
        }
    }
}

task integrationTest(type: Test) {
    description = 'Runs the integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
    mustRunAfter test
}

check.dependsOn integrationTest

// Download dependencies to Gradle's cache
task resolveDependencies {
  doLast {
    project.rootProject.allprojects.each { subProject ->
      subProject.buildscript.configurations.each { if (it.canBeResolved) it.resolve() }
      subProject.configurations.each { if (it.canBeResolved) it.resolve() }
    }
  }
}

task sourcesJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'Santander Parser'
                description = 'Library for parsing Santander UK credit card statements'
                url = 'https://github.com/clormor/santander-parser'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'clormor'
                        name = 'Chris Lormor'
                        email = 'cdlormor@gmail.com'
                    }
                }
                scm {
                    connection = 'https://github.com/clormor/santander-parser.git'
                    developerConnection = 'https://github.com/clormor/santander-parser.git'
                    url = 'https://github.com/clormor/santander-parser'
                }
            }
        }
    }
    repositories {
        maven {
            credentials {
                username mavenUser
                password mavenPassword
            }
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
